#
# source: https://github.com/Eriner/zim
# startup file read in interactive login shells
#
# The following code helps us by optimizing the existing framework.
# This includes zcompile, zcompdump, etc.
#

(
  # Function to determine the need of a zcompile. If the .zwc file
  # does not exist, or the base file is newer, we need to compile.
  # These jobs are asynchronous, and will not impact the interactive shell
  zcompare() {
    if [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
      zcompile ${1}
    fi
  }

  setopt extendedglob

  # zcompile the completion cache; siginificant speedup, and .zshrc, .aliases
  zcompare ${ZDOTDIR}/.zcompdump
  zcompare ${ZDOTDIR}/.zprofile
  zcompare ${ZDOTDIR}/.zshenv
  zcompare ${ZDOTDIR}/.zshrc

  # zcompile all autloaded functions, if there are any
  if [ -d "${ZDOTDIR}/autoloaded" ]; then
    for file in ${ZDOTDIR}/autoloaded/**/^(*.zwc)(.); do
      zcompare ${file}
    done
  fi

  # with current setup, we can't use zcompare on plugins as they are stored
  # in read-only nix root
) &!
